// Ball Thing class 
class Ball extends Thing {
    color ballColor;
    float size;
    float radius;

    Ball(color ballColor, float size, float posX, int sceneIn) {
        this.ballColor = ballColor;
        this.size = size;
        this.radius = size / 2;
        this.initialize();
        this.position = new PVector(posX, height * random(0.3, 0.5));
        this.velocity = new PVector(0, 0);
        this.acceleration = new PVector(0, 0);
        this.grabbable = true;
        this.sceneIn = sceneIn;
        this.hasPhysics = true; // Ball has physics
        this.groundHeightOffset = radius;
    }

    // Display the ball
    void display() {
        strokeWeight(2);
        fill(ballColor);
        ellipse(this.position.x, this.position.y, size, size);
    }
    
    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("ballColor", this.ballColor);
        data.put("radius", this.radius);
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("ballColor")) this.ballColor = ((Number) data.get("ballColor")).intValue();
        if (data.containsKey("radius")) this.radius = ((Number) data.get("radius")).floatValue();
        this.size = this.radius * 2;
    }
}

class BouncyBall extends Ball implements Interactable {
    Boolean rebounding = false;
    String imagePath;
    
    BouncyBall(String imagePath, float size, float posX, float elasticity, int sceneIn) {
      super(color(255), size, posX, sceneIn);
      this.elasticity = elasticity;
      this.imagePath = imagePath;
      gameManager.imageManager.addImage(imagePath, imagePath, round(this.radius*2), round(this.radius*2));
      this.checkTouchRadius = 150; // check Things 70 px from self
      this.checkTouchY = true;
      this.groundHeightOffset = this.radius * 0.6;
    }
    void display() {
      push();
      imageMode(CENTER);
      image(gameManager.imageManager.getImage(imagePath), this.position.x, this.position.y);
      imageMode(CORNER);
      pop();
    }
    void update() {
      super.update();
      if (this.velocity.x == 0) {
        this.rebounding = false;
      }
    }
    void onTouch(Thing other, float distance) {
        if (other instanceof BouncyBall && !(((BouncyBall) other).rebounding) && (((BouncyBall) other).radius) + this.radius >= distance) {
            if (other.position.x >= this.position.x) {
                other.position.x = this.position.x + this.radius + 20;
                other.velocity.x = 2;
                this.velocity.x = -2;
            } else if (other.position.x < this.position.x) {
                other.position.x = this.position.x - this.radius - 20;
                other.velocity.x = -2;
                this.velocity.x = 2;
            }
            this.rebounding = true;
            ((BouncyBall) other).rebounding = true;
        }
    }
    void onGrab(Human human) {} void onRelease(Human human) {} boolean isGrabbable() { return true; }
    void onInteract(Human human) {
      int direction = (round(random(0,1)) == 1) ? 1 : -1;
      if (gameManager.keyManager.isKeyPressed(LEFT)) direction = -1;
      if (gameManager.keyManager.isKeyPressed(RIGHT)) direction = 1;
      human.velocity.x *= -(direction);
      human.release();
      this.velocity.set(random(10, 160)*direction, random(-200, -600));
    }

    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("rebounding", this.rebounding);
        data.put("imagePath", this.imagePath);
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("rebounding")) this.rebounding = (boolean) data.get("rebounding");
        if (data.containsKey("imagePath")) {
            this.imagePath = (String) data.get("imagePath");
            // Re-add to ImageManager on load
            gameManager.imageManager.addImage(imagePath, imagePath, round(this.radius*2), round(this.radius*2));
        }
    }
}


// Shirt Thing class
class Shirt extends Thing implements Interactable {
    color shirtColor;

    Shirt(color shirtColor, float posX, int sceneIn) {
        this.shirtColor = shirtColor;
        this.initialize();
        this.position = new PVector(posX, height * 0.3);
        this.velocity = new PVector(0, 0);
        this.acceleration = new PVector(0, 0);
        this.elasticity = 0.75;
        this.grabbable = true;
        this.sceneIn = sceneIn;
        this.hasPhysics = true; // Shirt has physics
    }

    // Display the shirt - REQUIRED by abstract class Thing
    void display() {
            strokeWeight(2);
            fill(this.shirtColor);
            rect(this.position.x - 35, this.position.y - 64, 70, 100);
    }
    
    void onGrab(Human human) {
      gameManager.messageBox.showEvent("Grabbed a shirt! Press SHIFT to put on.");
    }

    // Interactable interface implementation
    void onInteract(Human human) {
        // Swap colors with human
        color orgColor = human.shirtColor;
        human.shirtColor = this.shirtColor;
        this.position.add(new PVector(25, 0));
        this.shirtColor = orgColor;
    }

    boolean isGrabbable() {
        return this.grabbable && !this.held;
    }

    void onRelease(Human human) {
    }
    
    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("shirtColor", this.shirtColor);
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("shirtColor")) this.shirtColor = ((Number) data.get("shirtColor")).intValue();
    }

}

// Chair Thing class
class Chair extends Thing implements Interactable {
    color chairColor;
    Thing restedThing;
    boolean humanOnChair = false;
    boolean occupied; // Whether chair is currently occupied (by human or Thing)

    Chair(color chairColor, float posX, int sceneIn) {
        this.chairColor = chairColor;
        this.initialize();
        this.position = new PVector(posX, height*gameManager.window.getGroundHeightAt(sceneIn, posX));
        this.velocity = new PVector(0, 0);
        this.elasticity = 0;
        this.grabbable = true;
        this.occupied = false;
        this.restedThing = null;
        this.sceneIn = sceneIn;
        this.checkTouchRadius = 150;
        this.checkTouchY = true;
        this.friction = 0.95;
        this.drawInBackground = true;
        // this.isStatic = true;
    }

    // Display the chair - REQUIRED by abstract class Thing
    void display() {
        strokeWeight(2);
        fill(chairColor);
        rect(this.position.x - 40, this.position.y - 60, 20, 92);
        rect(this.position.x + 20, this.position.y - 60, 20, 92);
        rect(this.position.x - 45, this.position.y - 60, 90, 20);
    }

    void putObjOnChair(Thing other) {
          other.rested = true;
          other.position.x = this.position.x;
          other.position.y = this.position.y - (other instanceof Ball ? (36 + ((Ball)other).radius * 1.075) : 80);
          other.velocity.x = 0;
          this.occupied = true;
          this.restedThing = other;
          this.held = false;
    }

    void onTouch(Thing other, float distance) {
      // HUMAN: do nothing
        if (other instanceof Human) return;
        
        if (abs(this.velocity.x) < 0.2) {
            if (millis() - other.lastReleasedMs < 360 && !this.occupied && other.position.y <= this.position.y) {
                putObjOnChair(other);
            } else if (millis() - other.lastReleasedMs < 360) {
                if (this.occupied) {
                    other.position.y = height*gameManager.window.getGroundHeightAt(position.x);
                    if (other.velocity.x == 0) {
                        other.position.x = other.position.x <= this.position.x ? this.position.x - 110 : this.position.x + 110;
                        other.velocity.x = other.position.x <= this.position.x ? -1.2 : 1.2;
                    } else {
                        other.velocity.x *= -0.8;
                    }
                } else {
                    other.velocity.y *= -0.8;
                    other.velocity.x *= -0.8;
                }
            }
        }

        // Clear occupied status if rested Thing is no longer resting
        if (restedThing == null || !restedThing.rested || restedThing.held) {
            this.occupied = false;
            this.humanOnChair = false;
            this.restedThing = null;
        }
    }

    void update() {
      super.update();
      if (restedThing == null || !restedThing.rested || restedThing.held || !restedThing.show) {
            this.occupied = false;
            this.humanOnChair = false;
            this.restedThing = null;
            this.held = false;
      }
    }

    // Press SHIFT to stand on chair
    void onInteract(Human human) {
      this.humanStand(human);
    }
    void humanStand(Human human) {
      if (human instanceof GameHuman) {
        GameHuman gameHuman = (GameHuman) human;
        gameManager.messageBox.showEvent("Human standing on chair!");
        
        // Calculate proper standing position
        float standX = this.position.x;
        float standY = this.position.y - 260; // Standing on the chair seat
        
        // Set human position properly
        gameHuman.position.x = standX;
        gameHuman.position.y = standY;
        
        gameHuman.standOnChair(this);
        this.occupied = true;
        this.restedThing = gameHuman;
        this.humanOnChair = true;
        
        // Make sure human releases the chair
        gameHuman.release();
        
        return;
      }
    }
    // Interactable interface implementation
    void onGrab(Human human) {
        this.held = true;
    }

    boolean isGrabbable() {
        return this.grabbable && !this.held && !this.occupied;
    }

    void onRelease(Human human) {
    }
    
    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("chairColor", this.chairColor);
        data.put("humanOnChair", this.humanOnChair);
        data.put("occupied", this.occupied);
        
        // Save reference to rested Thing if it exists and is Saveable
        if (this.restedThing != null && this.restedThing instanceof Saveable) {
            data.put("restedObjID", (this.restedThing).id);
        }
        
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("chairColor")) this.chairColor = ((Number) data.get("chairColor")).intValue();
        if (data.containsKey("humanOnChair")) this.humanOnChair = (boolean) data.get("humanOnChair");
        if (data.containsKey("occupied")) this.occupied = (boolean) data.get("occupied");
        
        // Load rested Thing reference
        if (data.containsKey("restedObjID")) {
            this.loadRestedObj(gameManager.things, ((Number) data.get("restedObjID")).intValue());
        }
    }
    
    void loadRestedObj(ArrayList<Thing> things, int objId) {
        if (objId > 0) {
            for (Thing thing : things) {
                if (thing instanceof Saveable && (thing).id == objId) {
                    this.restedThing = thing;
                    break;
                }
            }
        }
    }
}

// Door Thing class (for scene transitions)
class Door extends Thing implements Interactable {
    int sceneFrom; // Start scene
    int sceneDes; // Destination scene
    // Note: the two are interchangable.
    float posXFrom; // Previous X position
    float posXDes; // New X position in destination scene
    color frameColor;

    boolean showDoor = true;
    
    private int currentDisplayScene;
    private int targetScene;
    private float posXNew;
    
    boolean isOneWay;
    boolean sittingOnGround;

    Door(float posX, float posXDes, color frameColor, int sceneIn, int sceneDes) {
        this.initialize();
        this.position = new PVector(posX, height*gameManager.window.getGroundHeightAt(sceneIn, posX));
        this.velocity = new PVector(0, 0);
        this.elasticity = 0;
        this.grabbable = false;
        this.drawInBackground = true; 
        this.drawBehindHumans = true; // Behind humans
        this.sceneDes = sceneDes;
        this.sceneIn = sceneIn;
        this.sceneFrom = sceneIn;
        this.posXFrom = posX;
        this.posXDes = posXDes;
        this.targetScene = sceneDes;
        this.posXNew = posXDes;
        this.checkTouchRadius = 80;
        this.checkTouchY = true;
        this.isOneWay = false;
        this.sittingOnGround = true;
        this.frameColor = frameColor;
        this.hasPhysics = false; // Door is static
        this.updateInBackground = true;
    }

    void display() {
        if (this.showDoor) {
            push();
            checkDestination(this);
            fill(frameColor);
            strokeWeight(2);
            stroke(40);
            rect(this.position.x - 45, this.position.y - 255, 15, 292);
            rect(this.position.x + 30, this.position.y - 255, 15, 292);
            rect(this.position.x - 45, this.position.y - 255, 90, 15);
            rect(this.position.x - 45, this.position.y + 26, 90, 15);
            fill(gameManager.window.scenes.getAs(targetScene, Integer.class, this.frameColor));
            rect(this.position.x - 30, this.position.y - 240, 60, 265);
            strokeWeight(1);
            pop();
        }
    }

    void checkDestination(Thing other) {
        this.targetScene = (other.sceneIn == sceneFrom || this.isOneWay ? sceneDes : sceneFrom);
        this.posXNew = (other.sceneIn == sceneFrom || this.isOneWay ? posXDes : posXFrom);      
    }

    // Check collision with other Things - REQUIRED by abstract class Thing
    void onTouch(Thing other, float distance) {
        checkDestination(other);
        if (!(other instanceof Door) && !(other instanceof Human)) {
            other.position.x = posXNew < width / 2 ? posXNew + 120 + random(-20, 20) : posXNew - 120 - random(-20, 20);
            other.sceneIn = targetScene;
        }
    }

    // Interactable interface implementation
    void onGrab(Human human) {  
        checkDestination(human);
        human.sceneIn = targetScene;
        gameManager.messageBox.showEvent(human.name + " entered scene " + targetScene + "!");
        human.position.x = this.posXNew < width / 2 ? this.posXNew + 120 + random(-20, 20) : this.posXNew - 120 - random(-20, 20);
        human.grabbed = false;
    }
    void onInteract(Human human) {
      // There is no need to press SHIFT to enter door.
    }
    boolean isGrabbable() {
        // Doors are interactable but not "grabbable" in the traditional sense
        // They trigger immediately when interacted with
        return false;
    }

    void onRelease(Human human) {
        // Doors don't need to be released since they trigger immediately
    }
    
    void backgroundUpdate() {
        if (gameManager.window.scene == sceneFrom || (gameManager.window.scene == sceneDes && !isOneWay)) {
            if (this.sceneIn != gameManager.window.scene) {
                this.sceneIn = gameManager.window.scene;
                
                // Set correct position based on which scene we're in
                if (gameManager.window.scene == sceneFrom) {
                    this.position.x = posXFrom;
                    if (this.sittingOnGround) this.position.y = height*gameManager.window.getGroundHeightAt(this.position.x);

                } else {
                    this.position.x = posXDes;
                    if (this.sittingOnGround) this.position.y = height*gameManager.window.getGroundHeightAt(this.position.x);
                }
                
                this.showDoor = true;
            }
        } 
    }
    
    void update() {
        super.update();
        this.currentDisplayScene = gameManager.window.scene;
        // When in scene, make sure position is correct
        if (currentDisplayScene == sceneFrom) {
            this.position.x = posXFrom;
        } else if (currentDisplayScene == sceneDes) {
            this.position.x = posXDes;
        }
    }
    
    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("sceneFrom", this.sceneFrom);
        data.put("sceneDes", this.sceneDes);
        data.put("posXFrom", this.posXFrom);
        data.put("posXDes", this.posXDes);
        data.put("frameColor", this.frameColor);
        data.put("showDoor", this.showDoor);
        data.put("isOneWay", this.isOneWay);
        data.put("sittingOnGround", this.sittingOnGround);
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("sceneFrom")) this.sceneFrom = ((Number) data.get("sceneFrom")).intValue();
        if (data.containsKey("sceneDes")) this.sceneDes = ((Number) data.get("sceneDes")).intValue();
        if (data.containsKey("posXFrom")) this.posXFrom = ((Number) data.get("posXFrom")).floatValue();
        if (data.containsKey("posXDes")) this.posXDes = ((Number) data.get("posXDes")).floatValue();
        if (data.containsKey("frameColor")) this.frameColor = ((Number) data.get("frameColor")).intValue();
        if (data.containsKey("showDoor")) this.showDoor = (boolean) data.get("showDoor");
        if (data.containsKey("isOneWay")) this.isOneWay = (boolean) data.get("isOneWay");
        if (data.containsKey("sittingOnGround")) this.sittingOnGround = (boolean) data.get("sittingOnGround");
    }
}

// Cupboard/storage Thing class
class Cupboard extends Thing implements Interactable {
    boolean opened = false; // Whether cupboard is open
    color woodColor; // Color of the cupboard
    float cupboardHeight, cupboardWidth; // Dimensions
    ArrayList < Thing > cupboardItems = new ArrayList < Thing > (); // Things inside
    int sceneDes; // Destination scene for stored Things

    Cupboard(color woodColor, float x, float cupboardHeight, float cupboardWidth, int sceneIn, int sceneDes) {
        this.woodColor = woodColor;
        this.initialize();
        this.position = new PVector(x, height*gameManager.window.getGroundHeightAt(sceneIn, x));
        this.velocity = new PVector(0, 0);
        this.acceleration = new PVector(0, 0);
        this.elasticity = 0.4;
        this.grabbable = false;
        this.friction = 0.95;
        this.sceneIn = sceneIn;
        this.sceneDes = sceneDes;
        this.cupboardHeight = cupboardHeight;
        this.cupboardWidth = cupboardWidth;
        // this.isStatic = true; // Cupboard is *not* static - you pick it up to open it
    }

    // Display the cupboard - REQUIRED by abstract class Thing
    void display() {        
        strokeWeight(2);
        fill(woodColor);
        rect(position.x - cupboardWidth / 2, position.y - cupboardHeight + 36, cupboardWidth, cupboardHeight);

        fill(lerpColor(woodColor, color(40), 0.2));
        if (opened) {
            // Draw open doors
            quad(position.x - cupboardWidth / 2, position.y - cupboardHeight + 36,
                position.x - cupboardWidth, position.y - cupboardHeight + 26,
                position.x - cupboardWidth, position.y - 20 + 38,
                position.x - cupboardWidth / 2, position.y - 20 + 56);

            quad(position.x + cupboardWidth / 2, position.y - cupboardHeight + 36,
                position.x + cupboardWidth, position.y - cupboardHeight + 26,
                position.x + cupboardWidth, position.y - 20 + 38,
                position.x + cupboardWidth / 2, position.y - 20 + 56);
        } else {
            // Draw closed doors
            rect(position.x - cupboardWidth / 2 + 2, position.y - cupboardHeight + 38, cupboardWidth / 2 - 4, cupboardHeight - 4);
            rect(position.x + 2, position.y - cupboardHeight + 38, cupboardWidth / 2 - 4, cupboardHeight - 4);
        }
    }

    void onGrab(Human human) {
    }
    // Interactable interface implementation
    void onInteract(Human human) {
        if (!opened) {
            opened = true;
            // Move Things from storage scene to current scene
            for (Thing thing: cupboardItems) {
                if (thing != null) {
                    thing.sceneIn = this.sceneIn;
                    thing.position.x = this.position.x + this.cupboardWidth;
                }
            }
            cupboardItems.clear();
            gameManager.messageBox.showEvent("Cupboard opened");
            return;
        } else {
            cupboardItems.clear();
            // Check for Things to store
            for (Thing thing: gameManager.things) {
                if (thing != this) {
                    this.checkThing(thing);
                }
            }
            this.opened = false;
            gameManager.messageBox.showEvent("Cupboard closed");
        }
    }

    boolean isGrabbable() {
       return true;
    }

    void onRelease(Human human) {
        // Cupboards trigger immediately, no release needed
    }

    // Check if Thing should be stored in cupboard
    void checkThing(Thing other) {
        if (this.opened && !(other instanceof Cupboard) && !(other instanceof Door) && other.sceneIn == this.sceneIn) {
            float distance = PVector.dist(this.position, other.position);
            if (distance < 140 && !other.held) {
                other.position.x = this.position.x;
                cupboardItems.add(other);
                other.sceneIn = this.sceneDes;
                other.rested = false;
                other.velocity.x = 0;
            } else {
                cupboardItems.remove(other);
            }
        }
    }
    
    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("opened", this.opened);
        data.put("woodColor", this.woodColor);
        data.put("cupboardHeight", this.cupboardHeight);
        data.put("cupboardWidth", this.cupboardWidth);
        data.put("sceneDes", this.sceneDes);
        
        // Save shelf item IDs
        ArrayList<Integer> shelfIDs = new ArrayList<Integer>();
        for (Thing item : cupboardItems) {
            if (item instanceof Saveable) {
                shelfIDs.add((item).id);
            }
        }
        data.put("shelfIDs", shelfIDs);
        
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("opened")) this.opened = (boolean) data.get("opened");
        if (data.containsKey("woodColor")) this.woodColor = ((Number) data.get("woodColor")).intValue();
        if (data.containsKey("cupboardHeight")) this.cupboardHeight = ((Number) data.get("cupboardHeight")).floatValue();
        if (data.containsKey("cupboardWidth")) this.cupboardWidth = ((Number) data.get("cupboardWidth")).floatValue();
        if (data.containsKey("sceneDes")) this.sceneDes = ((Number) data.get("sceneDes")).intValue();
        
        // Load shelf references
        if (data.containsKey("shelfIDs")) {
            ArrayList<Integer> shelfIDs = (ArrayList<Integer>) data.get("shelfIDs");
            this.loadShelves(gameManager.things, shelfIDs);
        }
    }
    
    void loadShelves(ArrayList<Thing> things, ArrayList<Integer> shelfIDs) {
        this.cupboardItems.clear();
        for (int id : shelfIDs) {
            for (Thing thing : things) {
                if (thing instanceof Saveable && (thing).id == id) {
                    this.cupboardItems.add(thing);
                    break;
                }
            }
        }
    }
}

// Drone Thing class
class Drone extends Thing implements Interactable {
    float altitude = 10; // Current altitude
    float battery = 100; // Battery percentage
    String status = "Landed"; // Current status
    float verticalSpeed = 2; // Speed of vertical movement
    float oldBattery = 0; // Old battery value for charging
    color col; // Drone color

    String message = "Use WASD keys to control\nand use R key to recharge.";

    int upKey = 87; // W
    int downKey = 83; // S
    int leftKey = 65; // A
    int rightKey = 68; // D

    // Recharging system
    boolean recharging = false;
    int rechargeStartTime = 0;
    int rechargeDuration = 300; // Frames to fully recharge

    Drone(color c, float x, int sceneIn) {
        this.position = new PVector(x, height * 0.70);
        velocity = new PVector(0, 0);
        this.sceneIn = sceneIn;
        acceleration = new PVector(0, 0);
        this.grabbable = false;
        this.col = c;
        this.hasPhysics = true; // Drone has physics (it moves)
        this.drawInForeground = true;
    }

    // Display the drone and its status - REQUIRED by abstract class Thing
    void display() {
        float wobble = sin((frameCount/(frameRate/60)) * 0.1) * 2;
        float displayY = (this.held ? position.y : map(altitude, 0, 200, height - 50, 50) + (status.equals("Flying") ? wobble : 0));

        rectMode(CENTER);
        strokeWeight(2);
        fill(col);
        rect(position.x, displayY, 60, 30);
        rectMode(CORNER);

        // Draw status info
        fill(0);
        textSize(18);
        text("Status: " + status, width / 2.2, 55);
        text("Altitude: " + nf(altitude - 10, 0, 1) + " cm", width / 2.2, 85);
        text("Battery: " + int(battery) + "%", width / 2.2, 115);
        text(message, width / 2.3, 145);
    }
    
    void onGrab(Human human) {
      if (status.equals("Flying")) human.release();
    } 
    void onRelease(Human human) {} 
    void onInteract(Human human) {}
    boolean isGrabbable() { return !(status.equals("Flying")); };

    // Update drone state
    void update() {
        // Handle recharging
        if (gameManager.keyManager.isKeyPressed(82) && status.equals("Landed") && !recharging && battery < 100) {
            recharging = true;
            rechargeStartTime = (int)(frameCount/(frameRate/60));
            status = "Recharging";
            oldBattery = battery;
            gameManager.messageBox.showEvent("Recharging started...");
        }
        if (recharging) {
            int elapsed = (int)(frameCount/(frameRate/60)) - rechargeStartTime;
            battery = map(elapsed, 0, rechargeDuration, 0, 100) + oldBattery;
            battery = constrain(battery, 0, 100);
            if (elapsed >= rechargeDuration || battery == 100) {
                recharging = false;
                battery = 100;
                status = "Landed";
                gameManager.messageBox.showEvent("Recharging complete!");
            }
        }

        acceleration.set(0, 0);
        if (!status.equals("Landed") && !recharging && battery > 0) {
            if (gameManager.keyManager.isKeyPressed(leftKey)) acceleration.x = -0.6;
            if (gameManager.keyManager.isKeyPressed(rightKey)) acceleration.x = 0.6;
        }

        if (!this.held) {
            velocity.add(acceleration);
            velocity.x *= 0.95;
            velocity.x = constrain(velocity.x, -10, 10);
            position.add(velocity);

            // Wall collision
            if (position.x <= 30) {
                position.x = 30;
                velocity.x *= -0.8;
                gameManager.messageBox.showEvent("Hit left wall.");
            } else if (position.x >= width - 30) {
                position.x = width - 30;
                velocity.x *= -0.8;
                gameManager.messageBox.showEvent("Hit right wall.");
            }

            if (abs(velocity.x) < 0.1) velocity.x = 0;

            // Handle vertical movement
            if (battery > 0 && !status.equals("Emergency Landing") && !recharging) {
                if (gameManager.keyManager.isKeyPressed(upKey) && altitude < 200) {
                    altitude += verticalSpeed;
                    if (!held) {
                        if (status.equals("Landed")) status = "Flying";
                    } else {
                        status = "Landed";
                        this.position.y = height * 0.6;
                    }
                }
                if (gameManager.keyManager.isKeyPressed(downKey) && altitude > 0) {
                    altitude -= verticalSpeed;
                    if (altitude <= 10) {
                        altitude = 10;
                        status = "Landed";
                        velocity.set(0, 0);
                        this.sceneIn = 3;
                    }
                }
            }
        }

        // Battery drain when flying
        if (!status.equals("Landed") && !recharging) {
            battery -= 0.04;
            battery = max(battery, 0);
        }

        // Emergency landing when battery empty
        if (battery <= 0 && !status.equals("Landed") && !status.equals("Emergency Landing")) {
            gameManager.messageBox.showEvent("Battery empty! Emergency landing...");
            status = "Emergency Landing";
        }

        // Handle emergency landing
        if (status.equals("Emergency Landing")) {
            altitude -= verticalSpeed * 1.2;
            if (altitude <= 0) {
                altitude = 0;
                status = "Landed";
                velocity.set(0, 0);
                gameManager.messageBox.showEvent("Landed safely.");
            }
        }
    }

    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("altitude", this.altitude);
        data.put("battery", this.battery);
        data.put("status", this.status);
        data.put("verticalSpeed", this.verticalSpeed);
        data.put("oldBattery", this.oldBattery);
        data.put("col", this.col);
        data.put("recharging", this.recharging);
        data.put("rechargeStartTime", this.rechargeStartTime);
        data.put("rechargeDuration", this.rechargeDuration);
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("altitude")) this.altitude = ((Number) data.get("altitude")).floatValue();
        if (data.containsKey("battery")) this.battery = ((Number) data.get("battery")).floatValue();
        if (data.containsKey("status")) this.status = (String) data.get("status");
        if (data.containsKey("verticalSpeed")) this.verticalSpeed = ((Number) data.get("verticalSpeed")).floatValue();
        if (data.containsKey("oldBattery")) this.oldBattery = ((Number) data.get("oldBattery")).floatValue();
        if (data.containsKey("col")) this.col = ((Number) data.get("col")).intValue();
        if (data.containsKey("recharging")) this.recharging = (boolean) data.get("recharging");
        if (data.containsKey("rechargeStartTime")) this.rechargeStartTime = ((Number) data.get("rechargeStartTime")).intValue();
        if (data.containsKey("rechargeDuration")) this.rechargeDuration = ((Number) data.get("rechargeDuration")).intValue();
    }
}

// Lunchbox/food Thing class
class Lunchbox extends Thing implements Interactable {
    color boxColor; // Color of lunchbox
    float price; // Price to buy
    float energyValue; // Hunger reduction amount
    boolean consumed = false; // Whether food has been eaten
    String label; // Food type label

    Lunchbox(String label, color boxColor, float posX, int sceneIn, float price, float energyValue) {
        this.boxColor = boxColor;
        this.price = price;
        this.energyValue = energyValue;
        this.label = label;

        this.initialize();
        this.position = new PVector(posX, height*random(0.3, 0.6));
        this.velocity = new PVector(0, 0);
        this.acceleration = new PVector(0, 0);
        this.elasticity = 0.5;
        this.grabbable = true;
        this.sceneIn = sceneIn;
        this.friction = 0.9;
        this.groundHeightOffset = 26;
        this.hasPhysics = true; // Lunchbox has physics
    }

    // Display the lunchbox - REQUIRED by abstract class Thing
    void display() {
        if (!consumed) {
            textAlign(CENTER);
            strokeWeight(2);
            // Draw lunchbox body
            fill(boxColor);
            rect(this.position.x - 40, this.position.y - 15, 80, 50, 10);

            // Draw lunchbox latch
            fill(100);
            rect(this.position.x - 10, this.position.y - 20, 20, 10, 5);

            if (this.price > 0) {
              // Draw price tag
              fill(255);
              stroke(0);
              strokeWeight(1);
              rect(this.position.x - 25, this.position.y - 50, 50, 25, 5);
              
              // Draw price text
              fill(0);
              textSize(14);
              text("$" + nf(price, 0, 0), this.position.x, this.position.y - 34);
            }
            
            fill(0);
            // Draw energy value
            textSize(12);
            text("-" + nf(energyValue, 0, 0) + " hunger", this.position.x, this.position.y + 24);

            // Draw label
            fill(0);
            textSize(16);
            text(label, this.position.x, this.position.y + 5);

            // Reset text alignment
            textAlign(LEFT);
        }
    }

    // Interactable interface implementation
    void onGrab(Human human) {
      if (human instanceof GameHuman && !this.consumed) {
        GameHuman gameHuman = (GameHuman) human;
        // When grabbed, show info about the lunchbox
        gameManager.messageBox.showEvent("Lunchbox '" + label + "' grabbed by " + gameHuman.firstName);
        gameManager.messageBox.showEvent("Price: $" + price + ", Hunger reduction: " + energyValue);
        gameManager.messageBox.showEvent("Hold SHIFT while grabbing to eat this lunchbox!");
      }
    }

    boolean isGrabbable() {
        return this.grabbable && !this.held && !this.consumed;
    }

    void onRelease(Human human) {
    }

    
    // Add onInteract method
    void onInteract(Human human) {
      if (human instanceof GameHuman) {
        GameHuman gameHuman = (GameHuman) human;
        // Eat the lunchbox when SHIFT is pressed
        if (!consumed && gameHuman.money >= price) {
            // Consume the food
            gameHuman.eatFood(this);

            // Make sure hunger doesn't go below 0
            if (gameHuman.hunger < 0) {
                gameHuman.hunger = 0;
            }

            // Hide the food
            consumed = true;
            this.hide();

            gameManager.messageBox.showEvent(gameHuman.firstName + " ate " + label + "! Hunger: " + nf(gameHuman.hunger, 0, 1));
            
            // Release after eating
            if (gameHuman.grabThing == this) {
                gameHuman.release();
            }
        } else if (gameHuman.money < price) {
            gameManager.messageBox.showAlert("Not enough money! Need $" + price + ", but only have $" + nf(gameHuman.money, 0, 2));
        }
      }
    }

    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("boxColor", this.boxColor);
        data.put("price", this.price);
        data.put("energyValue", this.energyValue);
        data.put("consumed", this.consumed);
        data.put("label", this.label);
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("boxColor")) this.boxColor = ((Number) data.get("boxColor")).intValue();
        if (data.containsKey("price")) this.price = ((Number) data.get("price")).floatValue();
        if (data.containsKey("energyValue")) this.energyValue = ((Number) data.get("energyValue")).floatValue();
        if (data.containsKey("consumed")) this.consumed = (boolean) data.get("consumed");
        if (data.containsKey("label")) this.label = (String) data.get("label");
    }
}


// Cash bag with passcode protection
class CashBag extends Thing implements Interactable {
    color bagColor; // Color of cash bag
    float cashAmount; // Amount of money inside
    String passcode; // Passcode to unlock
    boolean unlocked = false; // Whether bag has been unlocked
    int wrongAttempts = 0; // Number of wrong passcode attempts
    int cooldownOnWrongAttempts = 3; // Trigger cooldown on this many wrong attempts
    int lastAttemptTime = 0; // Time of last attempt
    int cooldownTime = 2000; // 2 seconds cooldown after wrong attempt
    String feedbackMessage = ""; // Message to display
    int feedbackTime = 0; // Time feedback message was shown
    String hint = ""; // Hint to show in password box
    Human lastGrabbedHuman = null;
    
    // Input box for passcode entry
    InputBox passcodeInputBox;

    CashBag(color bagColor, float posX, int sceneIn, float cashAmount, String passcode, String hint) {
        this.bagColor = bagColor;
        this.cashAmount = cashAmount;
        this.passcode = passcode;
        this.hint = hint;
        
        this.initialize();
        this.position = new PVector(posX, height*0.4);
        this.velocity = new PVector(0, 0);
        this.acceleration = new PVector(0, 0);
        this.elasticity = 0.3;
        this.grabbable = false;
        this.sceneIn = sceneIn;
        this.friction = 0.85;
        this.groundHeightOffset = 36;
        
        // Create input box for passcode entry
        float boxWidth = 400;
        float boxHeight = 200;
        passcodeInputBox = new InputBox(
            width/2 - boxWidth/2, 
            height/2 - boxHeight/2,
            boxWidth,
            boxHeight,
            "ENTER PASSCODE",
            hint
        );
        passcodeInputBox.setMaxLength(40);
        passcodeInputBox.setPasswordMode(true);     
        
        // Customize the input box appearance
        passcodeInputBox.setColors(
            color(255),                    // bgColor
            bagColor,           // borderColor
            color(0),                     // textColor
            color(100, 100, 100)          // hintColor
        );

        passcodeInputBox.hideInstant();
    }

    // Display the cash bag - REQUIRED by abstract class Thing
    void display() {
        if (!unlocked) {
            // Draw cash bag
            fill(bagColor);
            stroke(0);
            strokeWeight(2);

            // Bag body
            rect(this.position.x - 40, this.position.y - 30, 80, 60, 15);

            // Draw dollar sign pattern
            fill(180, 255, 180);
            textSize(20);
            textAlign(CENTER);
            text("$", this.position.x, this.position.y - 5);

            // Draw amount
            fill(255);
            textSize(20);
            text(nf(cashAmount, 0, 0), this.position.x, this.position.y + 18);

            // Draw locked symbol
            fill(200, 0, 0);
            ellipse(this.position.x + 25, this.position.y - 15, 15, 15);
            fill(255);
            textSize(12);
            text("LOCKED", this.position.x, this.position.y - 35);

            // Draw wrong attempts counter if any
            if (wrongAttempts > 0) {
                fill(255, 100, 100);
                text("Wrong: " + wrongAttempts, this.position.x, this.position.y - 50);
            }

            // Draw feedback message if active
            if (millis() - feedbackTime < 3000 && !feedbackMessage.isEmpty()) {
                fill(255, 100, 100);
                textSize(14);
                text(feedbackMessage, this.position.x, this.position.y - 65);
            }

            // Reset text alignment
            textAlign(LEFT);
        } else if (unlocked) {
            // Draw opened/empty bag
            fill(150);
            stroke(0);
            strokeWeight(1);

            // Empty bag
            rect(this.position.x - 40, this.position.y - 30, 80, 60, 15);

            // Draw "EMPTY" text
            fill(100);
            textSize(18);
            textAlign(CENTER);
            text("EMPTY", this.position.x, this.position.y);

            // Draw money received message for a few seconds
            if (millis() - feedbackTime < 3000 && feedbackMessage.equals("CORRECT!")) {
                fill(100, 255, 100);
                textSize(14);
                text("+" + cashAmount, this.position.x, this.position.y + 20);
            } else if (millis() - feedbackTime > 5000 && feedbackMessage.equals("CORRECT!")) { this.hide(); // hide if empty for 5 seconds 
            }

            textAlign(LEFT);
        }
    }

    // Interactable interface implementation
    void onGrab(Human human) {
        lastGrabbedHuman = human;
        gameManager.messageBox.showEvent("Grabbed CashBag with " + cashAmount + "! Press SHIFT to unlock.");
    }
    
    void onInteract(Human human) {
        if (!unlocked) {
            // Check cooldown
            if (millis() - lastAttemptTime < cooldownTime && wrongAttempts >= cooldownOnWrongAttempts) {
                int remaining = (cooldownTime - (millis() - lastAttemptTime)) / 1000 + 1;
                feedbackMessage = "Cooldown: " + remaining + "s";
                feedbackTime = millis();
                gameManager.messageBox.showAlert("Try again in " + remaining + " seconds");
                return;
            }
            if (!passcodeInputBox.isVisible()) {
                passcodeInputBox.onSubmit = () -> {
                        checkPasscode(passcodeInputBox.getText());
                    };
                passcodeInputBox.onCancel = () -> {
                        gameManager.messageBox.showEvent("Passcode input cancelled");
                        feedbackMessage = "CANCELLED";
                        feedbackTime = millis();
                    };
                // Show the input box
                passcodeInputBox.show();
            }
        
        } else {
            gameManager.messageBox.showEvent("This cash bag is already empty!");
            feedbackMessage = "ALREADY EMPTY";
            feedbackTime = millis();
        }
    }

    boolean isGrabbable() {
      return true;
    }

    void onRelease(Human human) {
        lastGrabbedHuman = null;
    }

    // Check if entered passcode is correct
    void checkPasscode(String input) {
      if (lastGrabbedHuman != null && lastGrabbedHuman instanceof GameHuman) {
        GameHuman gameHuman = (GameHuman) lastGrabbedHuman;
        if (input.equals(passcode)) {
            // Correct passcode!
            unlocked = true;
            gameHuman.money += cashAmount;
            feedbackMessage = "CORRECT!";
            feedbackTime = millis();
            wrongAttempts = 0;

            gameManager.messageBox.showEvent("PASSCODE CORRECT! " + gameHuman.firstName + " gained $" + cashAmount);
            gameManager.messageBox.showEvent(gameHuman.firstName + " now has $" + nf(gameHuman.money, 0, 2));

            // Release if human was somehow holding this
            if (gameHuman.grabThing == this) {
                gameHuman.release();
            }
            
            // Hide the input box
            passcodeInputBox.hide();
        } else {
            // Wrong passcode
            wrongAttempts++;
            lastAttemptTime = millis();
            feedbackMessage = "WRONG! Attempts: " + wrongAttempts;
            feedbackTime = millis();

            gameManager.messageBox.showAlert("WRONG PASSCODE! Attempt " + wrongAttempts);
            
            // Clear the input box
            passcodeInputBox.setText("");
            
            gameHuman.release();
            
            // If too many wrong attempts, cooldown
            if (wrongAttempts >= cooldownOnWrongAttempts)  {
                cooldownTime = wrongAttempts * 2000; // Increase cooldown with attempts
                feedbackMessage = "ATTEMPTS EXCEEDED! Wait " + (cooldownTime/1000) + "s";
                passcodeInputBox.hide(); // Hide input box during cooldown
            }
        }
      }
    }

    // Update method to handle input box updates
    void update() {
        super.update();
    }
    
        @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("bagColor", this.bagColor);
        data.put("cashAmount", this.cashAmount);
        data.put("passcode", this.passcode);
        data.put("unlocked", this.unlocked);
        data.put("wrongAttempts", this.wrongAttempts);
        data.put("cooldownOnWrongAttempts", this.cooldownOnWrongAttempts);
        data.put("lastAttemptTime", this.lastAttemptTime);
        data.put("cooldownTime", this.cooldownTime);
        data.put("hint", this.hint);
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("bagColor")) this.bagColor = ((Number) data.get("bagColor")).intValue();
        if (data.containsKey("cashAmount")) this.cashAmount = ((Number) data.get("cashAmount")).floatValue();
        if (data.containsKey("passcode")) this.passcode = (String) data.get("passcode");
        if (data.containsKey("unlocked")) this.unlocked = (boolean) data.get("unlocked");
        if (data.containsKey("wrongAttempts")) this.wrongAttempts = ((Number) data.get("wrongAttempts")).intValue();
        if (data.containsKey("cooldownOnWrongAttempts")) this.cooldownOnWrongAttempts = ((Number) data.get("cooldownOnWrongAttempts")).intValue();
        if (data.containsKey("lastAttemptTime")) this.lastAttemptTime = ((Number) data.get("lastAttemptTime")).intValue();
        if (data.containsKey("cooldownTime")) this.cooldownTime = ((Number) data.get("cooldownTime")).intValue();
        if (data.containsKey("hint")) this.hint = (String) data.get("hint");
        
        // Reset UI state
        this.feedbackMessage = "";
        this.feedbackTime = 0;
        this.lastGrabbedHuman = null;
    }
}

class PreFilledCupboard extends Cupboard {
    String label = "Cupboard";
    
    PreFilledCupboard(String label, color woodColor, float x, float cupboardHeight, float cupboardWidth, 
           int sceneIn, int sceneDes, Thing[] customThings) {
        super(woodColor, x, cupboardHeight, cupboardWidth, sceneIn, sceneDes);
        this.cupboardItems = new ArrayList<Thing>(Arrays.asList(customThings));
        this.label = label;
        for (int i = 0; i < cupboardItems.size(); i++) {
          gameManager.things.add(cupboardItems.get(i));
        }
    }
    
    void onGrab(Human human) {};
    void onInteract(Human human) {
        if (!opened) {
            opened = true;
            
            for (Thing item : cupboardItems) {
                if (item != null && (item.sceneIn == this.sceneDes)) {
                    item.sceneIn = this.sceneIn;
                    item.show = true;
                    float angle = random(TWO_PI);
                    float radius = random(100, 150);
                    item.position.x = this.position.x + cos(angle) * radius;
                    item.position.y = this.position.y + sin(angle) * 30;
                    
                    // Give a good toss so it's visible
                    item.velocity.set(random(-10, 10), random(-20, -30));
                    item.held = false;
                    item.rested = false;
                    
                    // Make sure items are drawn after cupboard by reordering in Things list
                    reorderItemsInFront();
                    
                }
            }
        } else {
            gameManager.messageBox.showEvent("Cupboard closed. Collecting surrounding items...");
            
            // Collect nearby items like normal cupboard
            for (Thing thing : gameManager.things) {
                    Thing item = thing;
                    if (item.sceneIn == this.sceneIn) {
                        float distance = PVector.dist(this.position, item.position);
                        if (distance < 150 && !item.held && item.hasPhysics) {
                          if (!cupboardItems.contains(item)) cupboardItems.add(item);
                            item.sceneIn = this.sceneDes;
                            item.show = false;
                            item.velocity.set(0, 0);
                            item.position.x = this.position.x;
                            item.position.y = this.position.y - 30;
                        }
                    }
            }
            
            this.opened = false;
        }
    }
    
    // Reorder items to be drawn after cupboard in Things list
    void reorderItemsInFront() {
        // Remove cupboard from Things temporarily
        gameManager.things.remove(this);
        
        // Re-add cupboard FIRST (so it draws first, in background)
        gameManager.things.add(0, this);
        
        // Now items will be drawn after cupboard
    }
    
    @Override
    void display() {
        strokeWeight(2);
        // Draw cupboard body
        fill(woodColor);
        rect(position.x - cupboardWidth / 2, position.y - cupboardHeight + 36, 
             cupboardWidth, cupboardHeight);
        
        // Draw doors
        fill(lerpColor(woodColor, color(40), 0.2));
        if (opened) {
            // Draw open doors
            quad(position.x - cupboardWidth / 2, position.y - cupboardHeight + 36,
                position.x - cupboardWidth, position.y - cupboardHeight + 26,
                position.x - cupboardWidth, position.y - 20 + 38,
                position.x - cupboardWidth / 2, position.y - 20 + 56);

            quad(position.x + cupboardWidth / 2, position.y - cupboardHeight + 36,
                position.x + cupboardWidth, position.y - cupboardHeight + 26,
                position.x + cupboardWidth, position.y - 20 + 38,
                position.x + cupboardWidth / 2, position.y - 20 + 56);
        } else {
            // Draw closed doors with handles
            rect(position.x - cupboardWidth / 2 + 2, position.y - cupboardHeight + 38, 
                 cupboardWidth / 2 - 4, cupboardHeight - 4);
            rect(position.x + 2, position.y - cupboardHeight + 38, 
                 cupboardWidth / 2 - 4, cupboardHeight - 4);
            
            // Draw handles
            fill(100);
            ellipse(position.x - cupboardWidth/4, 
                    position.y - cupboardHeight/2, 
                    12, 20);
            ellipse(position.x + cupboardWidth/4, 
                    position.y - cupboardHeight/2, 
                    12, 20);
        }
        
        // Draw label
        fill(50);
        textSize(14);
        textAlign(CENTER);
        text(this.label, position.x, position.y - cupboardHeight + 20);
        textAlign(LEFT);
    }
    
    @Override
    HashMap<String, Object> save() {
        HashMap<String, Object> data = super.save();
        data.put("label", this.label);
        
        // Save cupboard item IDs
        ArrayList<Integer> itemIDs = new ArrayList<Integer>();
        for (Thing item : cupboardItems) {
            if (item instanceof Saveable) {
                itemIDs.add((item).id);
            }
        }
        data.put("cupboardItemIDs", itemIDs);
        
        return data;
    }
    
    @Override
    void load(HashMap<String, Object> data) {
        super.load(data);
        if (data.containsKey("label")) this.label = (String) data.get("label");
        
        // Load cupboard item references
        if (data.containsKey("cupboardItemIDs")) {
            ArrayList<Integer> itemIDs = (ArrayList<Integer>) data.get("cupboardItemIDs");
            this.loadCupboardItems(gameManager.things, itemIDs);
        }
    }
    
    void loadCupboardItems(ArrayList<Thing> things, ArrayList<Integer> itemIDs) {
        this.cupboardItems.clear();
        for (int id : itemIDs) {
            for (Thing thing : things) {
                if (thing instanceof Saveable && (thing).id == id) {
                    this.cupboardItems.add(thing);
                    break;
                }
            }
        }
    }
}

class SpeedBooster extends Thing implements Interactable {
  float boostMultiplier = 2.0;
  color boosterColor;
  boolean active = true;
  
  SpeedBooster(float x, int scene, float multiplier) {
    super();
    this.initialize(x, height * 0.5);
    this.sceneIn = scene;
    this.boostMultiplier = multiplier;
    this.boosterColor = color(0, 255, 255); // Cyan
    this.grabbable = true;
    this.checkTouchRadius = 30;
  }
  
  void display() {
    if (active) {
      // Pulsing effect
      float pulse = sin(millis() * 0.01) * 0.2 + 0.8;
      
      pushMatrix();
      translate(position.x, position.y);
      scale(pulse);
      
      // Glow effect
      for (int i = 3; i > 0; i--) {
        fill(red(boosterColor), green(boosterColor), blue(boosterColor), 50 / i);
        noStroke();
        ellipse(0, 0, 40 + i * 10, 40 + i * 10);
      }
      
      // Main body
      fill(boosterColor);
      stroke(255);
      strokeWeight(2);
      
      // Lightning bolt shape
      beginShape();
      vertex(-15, -15);
      vertex(0, -5);
      vertex(-5, 5);
      vertex(10, 15);
      vertex(0, 5);
      vertex(5, -5);
      vertex(-15, -15);
      endShape();
      
      // "x2" text
      fill(255);
      textSize(14);
      textAlign(CENTER);
      text("x" + int(boostMultiplier), 0, -25);
      
      popMatrix();
    }
  }
  
  void onGrab(Human human) {
    if (human instanceof GameHuman && active) {
      GameHuman gh = (GameHuman) human;
      
      // Apply boost
      gh.speed *= boostMultiplier;
      active = false;
      
      // Visual feedback
      gameManager.messageBox.showEvent("SPEED BOOST! " + boostMultiplier + "x faster!");
    
      gh.release(); // Release immediately after grabbing
      gameManager.things.remove(this); // Remove from Things list
    }
  }
  
  boolean isGrabbable() { return active; }
  void onInteract(Human human) {}
  void onRelease(Human human) {}
  
  @Override
  HashMap<String, Object> save() {
      HashMap<String, Object> data = super.save();
      data.put("boostMultiplier", this.boostMultiplier);
      data.put("boosterColor", this.boosterColor);
      data.put("active", this.active);
      return data;
  }
  
  @Override
  void load(HashMap<String, Object> data) {
      super.load(data);
      if (data.containsKey("boostMultiplier")) this.boostMultiplier = ((Number) data.get("boostMultiplier")).floatValue();
      if (data.containsKey("boosterColor")) this.boosterColor = ((Number) data.get("boosterColor")).intValue();
      if (data.containsKey("active")) this.active = (boolean) data.get("active");
  }
}
